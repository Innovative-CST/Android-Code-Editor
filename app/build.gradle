plugins {
    id "com.android.application"
}

def KEYSTOREPASSWORD = System.getenv('KEYSTOREPASSWORD')
def secretsPropertiesFile = file('../secrets.properties')
def secretsProperties = new Properties()
def secrets = System.getenv("GOOGLE_ANALYTICS")
def analyticsEnabled = secrets != null
def useEnvForFirebase = true

if (secretsPropertiesFile.exists()) {
    secretsProperties.load(new FileInputStream(secretsPropertiesFile))
}

if (analyticsEnabled) {
    useEnvForFirebase = true
} else if (secretsPropertiesFile.exists()) {
    if (secretsProperties.getProperty('useFirebase') != null) {
        analyticsEnabled = secretsProperties.getProperty('useFirebase') == "true"
        if (analyticsEnabled) {
            useEnvForFirebase = false
        }
    }
}

if (analyticsEnabled) {
    apply plugin: "com.google.gms.google-services"
}


def signingEnabled = false
def password = ""

if (KEYSTOREPASSWORD != null) {
    password = KEYSTOREPASSWORD
    signingEnabled = true
} else if (secretsPropertiesFile.exists()) {
    if (secretsProperties.getProperty('keyStorePassword') != null) {
        password = secretsProperties.getProperty('keyStorePassword')
        signingEnabled = true
    }
}

android {
    compileSdk 32
    buildToolsVersion "33.0.2"
    useLibrary 'org.apache.http.legacy'
    namespace "android.code.editor"
    def gitCommitHash = "git rev-parse HEAD".execute().text.trim()

	def getCommitHash = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine "git", "rev-parse", "--short", "HEAD"
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    defaultConfig {
        applicationId "android.code.editor"
        minSdk 24
        targetSdk 34
        versionCode 2
        versionName "1.0-Snapshot-" + getCommitHash()
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        viewBinding true
    }
    
    signingConfigs {
        release {
            if (signingEnabled) {
                storeFile file("../keystore.jks")
                storePassword password
                keyAlias "AndroidCodeEditor"
                keyPassword password
            }
        }
        debug {
            if (signingEnabled) {
                storeFile file("../keystore.jks")
                storePassword password
                keyAlias "AndroidCodeEditor"
                keyPassword password
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar", "*.aar"])
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.coordinatorlayout:coordinatorlayout:1.2.0"
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'com.google.code.gson:gson:2.8.7'
    implementation 'com.squareup.okhttp3:okhttp:3.9.1'
    implementation 'com.termux.termux-app:terminal-view:0.117'
    implementation 'com.termux.termux-app:terminal-emulator:0.117'
    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    def editorGroupId = "io.github.Rosemoe.sora-editor"
    implementation platform("$editorGroupId:bom:0.22.0")
    implementation "$editorGroupId:editor"
    implementation "$editorGroupId:language-textmate"
    implementation 'jp.co.cyberagent.android:gpuimage:2.1.0'
    if (analyticsEnabled) {
        if (useEnvForFirebase) {
            new File("$projectDir/google-services.json").text = secrets;
        }
    }
    implementation platform('com.google.firebase:firebase-bom:32.2.3')
    implementation("com.google.firebase:firebase-analytics")

    implementation project(path:':markdown-viewer')
    implementation project(path:':editor')
    implementation project(path:':treeview')
    implementation project(path:':common')
}
